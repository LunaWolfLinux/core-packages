# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Taijian <taijian@posteo.de>
# Contributor: Sebastian Lau <lauseb644@gmail.com>
# Contributor: Damian01w <damian01w@gmail.com>
# Contributor: Padfoot <padfoot@exemail.com.au>

pkgname=plymouth
pkgver=22.02.122
pkgrel=1
pkgdesc='Graphical boot splash screen'
arch=('x86_64')
url='https://www.freedesktop.org/wiki/Software/Plymouth/'
license=('GPL2')
depends=('bash' 'cairo' 'cantarell-fonts' 'filesystem' 'glib2' 'glibc' 'libdrm' 'libpng' 'pango' 'systemd-libs')
makedepends=('gtk3' 'docbook-xsl')
optdepends=('gtk3: x11 renderer')
backup=('etc/plymouth/plymouthd.conf')
install='plymouth.install'
source=("https://www.freedesktop.org/software/$pkgname/releases/$pkgname-$pkgver.tar.xz"
        "plymouth-shutdown.patch::https://gitlab.freedesktop.org/plymouth/plymouth/-/commit/2f12aa4c5d27d1ee5a46f412559073f016d236ae.patch"
        'plymouth.initcpio_hook'
        'plymouth.initcpio_install')
sha256sums=('100551442221033ce868c447ad6c74d831d209c18ae232b98ae0207e34eadaeb'
            'cd35f5d5c6ceff353375500fbe4ea2293b5bfb13c3a02dab2c82c1a85b808288'
            '043142b0718d5b8f1b105d86d081fa31160f972bbc049199e2f5ea789b3115b4'
            '4fbfafac2662047e59c1a236b1c1fc20c68ee05e333e5fb27e220f3cda6b1ccf')

prepare() {
  cd "$pkgname-$pkgver"

  # Add mkinitcpio support to plymouth-switch-root-initramfs.service
  # https://gitlab.freedesktop.org/plymouth/plymouth/-/merge_requests/202
  patch -Np1 -i ../plymouth-shutdown.patch

  # Use mkinitcpio to update initrd
  sed -i 's/dracut -f/mkinitcpio -P/' scripts/plymouth-update-initrd

  # Change default theme
  sed -i 's/^Theme=spinner$/Theme=lunawolf/' src/plymouthd.defaults

  # Change default theme in config file
	cat > src/plymouthd.conf <<- EOF
		# Set your plymouth configuration here.
		[Daemon]
		Theme=lunawolf
		ShowDelay=0
		DeviceTimeout=8
	EOF
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --sbindir=/usr/bin --libexecdir=/usr/lib --sysconfdir=/etc \
              --localstatedir=/var --runstatedir=/run --with-runtimedir=/run \
              --with-logo=/usr/share/pixmaps/lunawolf-logo.png
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make

  # Convert logo for the spinner theme
  rsvg-convert '../../../artwork/lunawolf-logo.svg' -o ../lunawolf-logo.png
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir/var/run"

  # Install mkinitcpio hook
  install -Dm644 ../plymouth.initcpio_hook "$pkgdir/usr/lib/initcpio/hooks/$pkgname"
  install -Dm644 ../plymouth.initcpio_install "$pkgdir/usr/lib/initcpio/install/$pkgname"

  # Install logo for the spinner theme
  install -Dm644 ../lunawolf-logo.png "$pkgdir/usr/share/$pkgname/themes/spinner/watermark.png"
}
